STRIG
================

Reads button state from attached gaming devices

Syntax:
declare function Strig ( byval button as long ) as long

Usage:
result = Strig( button )

Parameters:

`button`
the button to query for state

Return Value:
Returns -1 (pressed) or 0 (not-pressed) to indicate the state of the `button`
requested.

Description:

`Strig` will retrieve the button state for the first and second buttons on the
first and second gaming devices. `button` must be a number between 0 and 7 and
has the following meaning:

Button State to return
0 First button on gaming device A pressed since STICK(0) was called
1 First button on gaming device A is pressed
2 First button on gaming device B pressed since STICK(0) was called
3 First button on gaming device B is pressed
4 Second button on gaming device A pressed since STICK(0) was called
5 Second button on gaming device A is pressed
6 Second button on gaming device B pressed since STICK(0) was called
7 Second button on gaming device B is pressed


Calling `Stick(0)` will reset the state returned where `button` is equal to 0,
2, 4, or 6.

Examples:

'' Compile with -lang qb

'$lang: "qb"

Screen 12

Do
    Locate 1, 1
    Print "Joystick A-X position : "; Stick(0); "   "
    Print "Joystick A-Y position : "; Stick(1); "   "
    Print "Joystick B-X position : "; Stick(2); "   "
    Print "Joystick B-Y position : "; Stick(3); "   "
    Print
    Print "Button A1 was pressed : "; Strig(0); "  "
    Print "Button A1 is pressed  : "; Strig(1); "  "
    Print "Button B1 was pressed : "; Strig(2); "  "
    Print "Button B1 is pressed  : "; Strig(3); "  "
    Print "Button A2 was pressed : "; Strig(4); "  "
    Print "Button A2 is pressed  : "; Strig(5); "  "
    Print "Button B2 was pressed : "; Strig(6); "  "
    Print "Button B2 is pressed  : "; Strig(7); "  "
    Print
    Print "Press ESC to Quit"

    If Inkey$ = Chr$(27) Then
        Exit Do
    End If

    Sleep 1

Loop


Dialect Differences:
Only available in the -lang qb dialect.

Differences from QB:
  * None

